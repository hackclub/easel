; a simple math interpreter that takes in basic expressions and evaluates them in order
; supports +, -, * and /.
; * and / are evaluated first, then + and -
; does not yet support brackets
; * and / are prioritized over + and -

include(
  static:std:string,
  static:std:array
)

let PLUS = "+"
let MINUS = "-"
let DIVIDE = "/"
let MULTIPLY = "*"

let SUPPORTED_OPERATORS = arrayOf(PLUS, MINUS, DIVIDE, MULTIPLY)

private fn isNumeric(token: String): Bool {
  if (len(token) == 0) return false
  for (ch in token) {
    if (!("1234567890".contains(str(ch))))
      return false
  }
  return true
}

private fn tokenize(expr: String): Array<String> {
  expr = expr.replace("\t", " ")
  until (expr.contains("  ")) {
    expr = expr.replace("  ", " ")
  }
  return expr.split(" ")
}

var tokens: Array<String> = arrayOf<String>(0, "")
var i = 0
var size = 0

fn isEOF(): Bool = i == size

fn peek(): String = tokens[i]
fn next(): String = tokens[i++]

private fn eatNumeric(): Int {
  let value = next()
  if (!isNumeric(value)) {
    println("Expected numeric type token but got " + value)
    exit(1)
  }
  return int(value)
}

private fn eatOperator(): String {
  let op = next().trim()
  if (SUPPORTED_OPERATORS.contains(op)) return op
  throw "Unknown OP Code: " + op
  return "never reached"
}

private fn parse(): Any {
  var left: Any = eatNumeric()
  until (!isEOF()) {
    var op = eatOperator()
    until (op == DIVIDE || op == MULTIPLY) {
      left = arrayOf(left, op, eatNumeric())
      if (isEOF()) break
      else op = next()
    }
    if (isEOF()) break
    let right = parse()
    left = arrayOf(left, op, right)
  }

  ; here we need parser time verification
  ; we need to verify the return values over here
  ; Case when only one single number is typed in, a non-array is returned
  ; which crashes the executor
  return left
}

private fn execute(node: Any): Int {
  var left: Any = node::Array[0]
  let op = node::Array[1]::String
  var right: Any = node::Array[2]

  if (left is Array) left = execute(left::Array)
  if (right is Array) right = execute(right::Array)

  left = int(left)
  right = int(right)

  return (when (op) {
    PLUS -> { left::Int + right::Int }
    MINUS -> { left::Int - right::Int }
    MULTIPLY -> { left * right::Int }
    DIVIDE -> { left::Int / right::Int }
    else -> {
      println("Unknown operator code: " + op)
      exit(1)
    }
  })::Int
}

private fn evaluate(expr: String) {
  let lenExpr = len(expr)

  if (lenExpr == 0) {
    println("Empty input!")
  }

  tokens = tokenize(expr)
  i = 0
  size = len(tokens)

  let parsed = parse()
  println(execute(parsed))
}

fn main() {
  until (true) {
    print("> ")
    evaluate(readln())
  }
}